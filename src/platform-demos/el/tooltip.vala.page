<?xml version="1.0" encoding="utf-8"?>
<page xmlns="http://projectmallard.org/1.0/" xmlns:its="http://www.w3.org/2005/11/its" xmlns:xi="http://www.w3.org/2001/XInclude" type="guide" style="task" id="tooltip.vala" xml:lang="el">
  <info>
  <title type="text">Συμβουλή οθόνης (Vala)</title>
    <link type="guide" xref="beginner.vala#misc"/>
    <link type="seealso" xref="toolbar.vala"/>
    <revision version="0.1" date="2013-06-18" status="review"/>

    <credit type="author copyright">
      <name>Tiffany Antopolski</name>
      <email its:translate="no">tiffany.antopolski@gmail.com</email>
      <years>2013</years>
    </credit>

    <desc>Προσθέστε συμβουλές στα γραφικά στοιχεία σας</desc>
  
    <mal:credit xmlns:mal="http://projectmallard.org/1.0/" type="translator copyright">
      <mal:name>Ελληνική μεταφραστική ομάδα GNOME</mal:name>
      <mal:email>team@gnome.gr</mal:email>
      <mal:years>2012-2014</mal:years>
    </mal:credit>
  
    <mal:credit xmlns:mal="http://projectmallard.org/1.0/" type="translator copyright">
      <mal:name>Δημήτρης Σπίγγος</mal:name>
      <mal:email>dmtrs32@gmail.com</mal:email>
      <mal:years>2012, 2013</mal:years>
    </mal:credit>
  
    <mal:credit xmlns:mal="http://projectmallard.org/1.0/" type="translator copyright">
      <mal:name>Μαρία Θουκιδίδου</mal:name>
      <mal:email>marablack3@gmail.com</mal:email>
      <mal:years>2014</mal:years>
    </mal:credit>
  
    <mal:credit xmlns:mal="http://projectmallard.org/1.0/" type="translator copyright">
      <mal:name>Θάνος Τρυφωνίδης</mal:name>
      <mal:email>tomtryf@gmail.com</mal:email>
      <mal:years>2014</mal:years>
    </mal:credit>
  </info>

  <title>Συμβουλή οθόνης</title>
  <media type="image" mime="image/png" src="media/tooltip.png"/>
  <p>Μια εργαλειοθήκη με μια συμβουλή οθόνης (με μια εικόνα) για ένα κουμπί.</p>
  <note><p>Αυτό το παράδειγμα δομεί στο παράδειγμα <link xref="toolbar.vala">εργαλειοθήκη</link>.</p></note>

  <links type="section"/>
    
  <section id="code">
  <title>Ο χρησιμοποιούμενος κώδικας για παραγωγή αυτού παραδείγματος</title>
    <code mime="text/x-csharp" style="numbered">/* Αυτό είναι το παράθυρο */
class MyWindow : Gtk.ApplicationWindow {

	/* Μεταβλητές στιγμιοτύπου που ανήκουν στο παράθυρο */
	Gtk.Toolbar toolbar;
	Gtk.ToolButton new_button;
	Gtk.ToolButton open_button;
	Gtk.ToolButton undo_button;
	Gtk.ToolButton fullscreen_button;
	Gtk.ToolButton leave_fullscreen_button;

	/* Κατασκευαστής */
	internal MyWindow (MyApplication app) {
		Object (application: app, title: "Toolbar with Tooltips Example");

		this.set_default_size (400, 200);
		var grid = new Gtk.Grid ();
		this.add (grid);
		grid.show ();

		create_toolbar ();
		toolbar.set_hexpand (true);
		grid.attach (toolbar, 0, 0, 1, 1);
		toolbar.show ();

		/* δημιουργία της ενέργειας παραθύρου "undo" */
		var undo_action = new SimpleAction ("undo", null);
		undo_action.activate.connect (undo_callback);
		this.add_action (undo_action);

		/* δημιουργία της ενέργειας παραθύρου "fullscreen" */
		var fullscreen_action = new SimpleAction ("fullscreen", null);
		fullscreen_action.activate.connect (fullscreen_callback);
		this.add_action (fullscreen_action);
	}

	/* Η συνάρτηση επανάκλησης για το σήμα query_tooltip */
	bool undo_tooltip_callback (int x, int y, bool keyboard_tooltip, Gtk.Tooltip tooltip) {

		/* ορισμός του κειμένου για τη συμβουλή οθόνης */
        	tooltip.set_text ("Undo your last action");
		
		/* ορισμός εικονιδίου για τη συμβουλή οθόνης */
		tooltip.set_icon_from_stock("gtk-undo", Gtk.IconSize.MENU);

        	/* εμφάνιση της συμβουλής οθόνης */
        	return true;
	}

	/* Αυτή η συνάρτηση δημιουργεί την εργαλειοθήκη, τα κουμπιά εργαλείων της,
	 * και εκχωρεί τα ονόματα ενεργειών στα κουμπιά εργαλείων.*/
	void create_toolbar () {
		toolbar = new Gtk.Toolbar ();
		toolbar.get_style_context ().add_class (Gtk.STYLE_CLASS_PRIMARY_TOOLBAR);

		new_button = new Gtk.ToolButton.from_stock (Gtk.Stock.NEW);

		/* συμβουλή οθόνης με κείμενο */
		new_button.set_tooltip_text ("Create a new file");

		new_button.is_important = true; //decides whether to show the label
		toolbar.add (new_button);
		new_button.show ();
		new_button.action_name = "app.new";

		open_button = new Gtk.ToolButton.from_stock (Gtk.Stock.OPEN);

		/* μια συμβουλή οθόνης χρησιμοποιώντας τη γλώσσα σήμανσης Pango */
		open_button.set_tooltip_markup ("Open an &lt;i&gt;existing&lt;/i&gt; file");

		open_button.is_important = true;
		toolbar.add (open_button);
		open_button.show ();
		open_button.action_name = "app.open";

		undo_button = new Gtk.ToolButton.from_stock (Gtk.Stock.UNDO);
	
		/* Για να έχει μια συμβουλή οθόνης μια εικόνα, πρέπει πρώτα set_has_tooltip να είναι 'true' */	
		(undo_button as Gtk.Widget).set_has_tooltip (true);

		/* Σύνδεση με το σήμα query_tooltip στην επανάκληση */
		undo_button.query_tooltip.connect (undo_tooltip_callback);
	
		undo_button.is_important = true;
		toolbar.add (undo_button);
		undo_button.show ();
		undo_button.action_name = "win.undo";

		fullscreen_button = new Gtk.ToolButton.from_stock (Gtk.Stock.FULLSCREEN);
		fullscreen_button.is_important = true;
		toolbar.add (fullscreen_button);
		fullscreen_button.show ();
		fullscreen_button.action_name = "win.fullscreen";

		leave_fullscreen_button = new Gtk.ToolButton.from_stock (Gtk.Stock.LEAVE_FULLSCREEN)
;
		leave_fullscreen_button.is_important = true;
		toolbar.add (leave_fullscreen_button);

		leave_fullscreen_button.action_name = "win.fullscreen";
	}

	void undo_callback (SimpleAction simple, Variant? parameter) {
			print ("You clicked \"Undo\".\n");
	}

	void fullscreen_callback (SimpleAction simple, Variant? parameter) {
		if ((this.get_window ().get_state () &amp; Gdk.WindowState.FULLSCREEN) != 0) {
			this.unfullscreen ();
			leave_fullscreen_button.hide ();
			fullscreen_button.show ();
		}
		else {
			this.fullscreen ();
			fullscreen_button.hide ();
			leave_fullscreen_button.show ();
		}
	}
}

/* Αυτή είναι η εφαρμογή */
class MyApplication : Gtk.Application {
	protected override void activate () {
		new MyWindow (this).show ();
	}

	protected override void startup () {
		base.startup ();

		/* Δημιουργία της ενέργειας "new" και προσθήκη της στην app*/
		var new_action = new SimpleAction ("new", null);
		new_action.activate.connect (new_callback);
		this.add_action (new_action);

		/* Δημιουργία της ενέργειας "open" και προσθήκη της στην app */
		var open_action = new SimpleAction ("open", null);
		open_action.activate.connect (open_callback);
		this.add_action (open_action);

		/* Μπορείτε επίσης να προσθέσετε την ενέργεια στο μενού app
		 * αν θέλετε.
		 */
		//var menu = new Menu ();
		//menu.append ("New", "app.new");
		//this.app_menu = menu;
	}

	void new_callback (SimpleAction action, Variant? parameter) {
		print ("You clicked \"New\".\n");
	}

	void open_callback (SimpleAction action, Variant? parameter) {
			print ("You clicked \"Open\".\n");
	}
}

/* Η κύρια συνάρτηση δημιουργεί την εφαρμογή και την εκτελεί. */
int main (string[] args) {
	return new MyApplication ().run (args);
}
</code>
  </section>

  <section id="references">
  <title>Αναφορές API</title>
    <p>Σε αυτό το παράδειγμα χρησιμοποιήσαμε τα παρακάτω:</p>
    <list>

      <item><p><link href="http://www.valadoc.org/gtk+-3.0/Gtk.Tooltip.html">Συμβουλή οθόνης Gtk (Gtk.Tooltip)</link></p></item>
      <item><p><link href="http://www.valadoc.org/gtk+-3.0/Gtk.Toolbar.html">Gtk.Toolbar</link></p></item>
      <item><p><link href="http://www.valadoc.org/gtk+-3.0/Gtk.Widget.query_tooltip.html">Συμβουλή οθόνης ερωτήματος γραφικού στοιχείου Gtk (Gtk.Widget.query_tooltip)</link></p></item>
      <item><p><link href="https://developer.gnome.org/gtk3/3.4/gtk3-Stock-Items.html#GTK-STOCK-UNDO:CAPS">Έτοιμα στοιχεία (Stock Items)</link></p></item>
    </list>
  </section>
</page>
