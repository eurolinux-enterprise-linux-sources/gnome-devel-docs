<?xml version="1.0" encoding="utf-8"?>
<page xmlns="http://projectmallard.org/1.0/" xmlns:its="http://www.w3.org/2005/11/its" xmlns:xi="http://www.w3.org/2001/XInclude" type="guide" style="task" id="radiobutton.vala" xml:lang="cs">
  <info>
  <title type="text">RadioButton (Vala)</title>
    <link type="guide" xref="beginner.vala#buttons"/>
    <link type="seealso" xref="togglebutton.vala"/>
    <link type="seealso" xref="grid.vala"/>

    <revision version="0.1" date="2012-05-09" status="draft"/>

    <credit type="author copyright">
      <name>Tiffany Antopolski</name>
      <email its:translate="no">tiffany.antopolski@gmail.com</email>
      <years>2012</years>
    </credit>

    <desc>Volba z více zaškrtávacích políček</desc>
  </info>

  <title>RadioButton</title>
  <media type="image" mime="image/png" src="media/radiobutton.png"/>
  <p>Tyto skupinové přepínače oznamují svoji aktivitu v terminálu.</p>

<code mime="text/x-csharp" style="numbered">public class MyWindow : Gtk.ApplicationWindow {
	internal MyWindow (MyApplication app) {
		Object (application: app, title: "RadioButton Example");

		this.border_width = 20;
		this.set_default_size (250, 100);

		/* Ukážeme si 3 různé metody vytvoření RadioButton */

		// Vytvoří Radio Button
		var button1 = new Gtk.RadioButton (null);
		button1.set_label ("Button 1");

		// Vytvoří RadioButton s popiskem a přidá jej do stejné skupiny, jako je button1
		var button2 = new Gtk.RadioButton.with_label (button1.get_group(),"Button 2");

		// Vytvoří RadioButton s popiskem a přidá jej do skupiny skupinového přepínač button1
		var button3 = new Gtk.RadioButton.with_label_from_widget (button1, "Button 3");

		// Připojí skupinové přepínače do mřížky
		var grid = new Gtk.Grid ();
		grid.attach (button1, 0, 0, 1, 1);
		grid.attach (button2, 0, 1, 1, 1);
		grid.attach (button3, 0, 2, 1, 1);

		// Přidá mřížku do okna
		this.add (grid);

		// Napojí obsluhu signálu (neboli funkce zpětného volání) ke skupinovým přepínačům
		button1.toggled.connect (button_toggled_cb);
		button2.toggled.connect (button_toggled_cb);
		button3.toggled.connect (button_toggled_cb);
	}

	void button_toggled_cb (Gtk.ToggleButton button)
	{
		var state = "unknown";

		if (button.get_active ())
			state = "on";
		else {
			state = "off";
			print ("\n");
		}
		print (button.get_label() + " was turned " + state + "\n");
	}
}

public class MyApplication : Gtk.Application {
	protected override void activate () {

		// Vše zobrazí
		new MyWindow (this).show_all ();
	}

	internal MyApplication () {
		Object (application_id: "org.example.MyApplication");
	}
}

public int main (string[] args) {
	return new MyApplication ().run (args);
}
</code>
<p>V této ukázce se používá následující:</p>
<list>
  <item><p><link href="http://www.valadoc.org/gtk+-3.0/Gtk.RadioButton.html">Gtk.RadioButton</link></p></item>
</list>
</page>
