<?xml version="1.0" encoding="utf-8"?>
<page xmlns="http://projectmallard.org/1.0/" xmlns:its="http://www.w3.org/2005/11/its" xmlns:xi="http://www.w3.org/2001/XInclude" type="guide" style="task" id="combobox.vala" xml:lang="cs">
  <info>
  <title type="text">ComboBox (Vala)</title>
    <link type="guide" xref="beginner.vala#menu-combo-toolbar"/>
    <revision version="0.1" date="2012-05-07" status="draft"/>

    <credit type="author copyright">
      <name>Tiffany Antopolski</name>
      <email its:translate="no">tiffany.antopolski@gmail.com</email>
      <years>2012</years>
    </credit>

    <desc>Widget, který se používá k výběru položek ze seznamu.</desc>
  </info>

  <title>ComboBox</title>
  <media type="image" mime="image/png" src="media/combobox.png"/>
  <p>Tento rozbalovací seznam vypisuje změnu svého stavu do terminálu.</p>

<code mime="text/x-csharp" style="numbered">/* Okno v aplikaci */
class MyWindow : Gtk.ApplicationWindow {

	/* Instance pole s linuxovými distribucemi náležející do tohoto okna */
	string[] distros = {"Select distribution", "Fedora", "Mint", "Suse"};

	/* Tento výčet dělá kód lépe čitelným, když se odkazujeme na sloupec
	 * jako Column.DISTRO namísto 0.
	 */
	enum Column {
		DISTRO
	}

	/* Konstruktor */
	internal MyWindow (MyApplication app) {
		Object (application: app, title: "Welcome to GNOME");

		this.set_default_size (200, -1);
		this.border_width = 10;

		Gtk.ListStore liststore = new Gtk.ListStore (1, typeof (string));

		for (int i = 0; i &lt; distros.length; i++){
			Gtk.TreeIter iter;
			liststore.append (out iter);
			liststore.set (iter, Column.DISTRO, distros[i]);
		}

		Gtk.ComboBox combobox = new Gtk.ComboBox.with_model (liststore);
		Gtk.CellRendererText cell = new Gtk.CellRendererText ();
		combobox.pack_start (cell, false);

		combobox.set_attributes (cell, "text", Column.DISTRO);

		/* Nastaví první položku v seznamu jako vybranou (aktivní) */
		combobox.set_active (0);

		/* Napojí signál "changed" rozbalovacího seznamu
		 * na obsluhu signálu (neboli funkci zpětného volání)
		 */
		combobox.changed.connect (this.item_changed);

		/* Přidá rozbalovací seznam do tohoto okna */
		this.add (combobox);
		combobox.show ();
	}

	/* Zpracování signálu pro signál "changed" rozbalovacího seznamu */
	void item_changed (Gtk.ComboBox combo) {
		if (combo.get_active () !=0) {
			print ("You chose " + distros [combo.get_active ()] +"\n");
		}
	}
}

/* Toto je aplikace */
class MyApplication : Gtk.Application {

	/* Konstruktor */
	internal MyApplication () {
		Object (application_id: "org.example.MyApplication");
	}

	/* Přepíše signál "activate" z GLib.Application,
	 * který je zděděný z Gtk.Application
	 */
	protected override void activate () {

		/* Vytvoří okno této aplikace a zobrazí jej */
		new MyWindow (this).show ();
	}
}

/* main vytvoří a spustí aplikaci */
int main (string[] args) {
	return new MyApplication ().run (args);
}
</code>
<p>V této ukázce se používá následující:</p>
<list>
  <item><p><link href="http://www.valadoc.org/gtk+-3.0/Gtk.ListStore.html">Gtk.ListStore</link></p></item>
  <item><p><link href="http://www.valadoc.org/gtk+-3.0/Gtk.ComboBox.html">Gtk.ComboBox</link></p></item>
  <item><p><link href="http://www.valadoc.org/gtk+-3.0/Gtk.CellRendererText.html">Gtk.CellRendererText</link></p></item>
  <item><p><link href="http://www.valadoc.org/gtk+-3.0/Gtk.CellLayout.set_attributes.html">set_attributes</link></p></item>
</list>
</page>
