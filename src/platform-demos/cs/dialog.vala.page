<?xml version="1.0" encoding="utf-8"?>
<page xmlns="http://projectmallard.org/1.0/" xmlns:its="http://www.w3.org/2005/11/its" xmlns:xi="http://www.w3.org/2001/XInclude" type="guide" style="task" id="dialog.vala" xml:lang="cs">
  <info>
  <title type="text">Dialog (Vala)</title>
    <link type="guide" xref="beginner.vala#windows"/>
    <revision version="0.1" date="2012-04-07" status="stub"/>

    <credit type="author copyright">
      <name>Tiffany Antopolski</name>
      <email its:translate="no">tiffany.antopolski@gmail.com</email>
      <years>2012</years>
    </credit>

    <desc>Vyskakovací okno</desc>
  </info>

  <title>Dialog</title>
  <media type="image" mime="image/png" src="media/dialog.png"/>
  <p>Dialogové okno reagující na signál zaháčkovaný na funkci zpětného volání.</p>

      <code mime="text/x-csharp" style="numbered">
/* Okno v aplikaci */
public class MyWindow : Gtk.ApplicationWindow {

	/* Konstruktor */
	internal MyWindow (MyApplication app) {
		Object (application: app, title: "GNOME Button");

		this.window_position = Gtk.WindowPosition.CENTER;
		this.set_default_size (250,50);

		var button = new Gtk.Button.with_label ("Click Me");

		/* Napojí signál "clicked" tlačítka
		 * na obsluhu signálu (neboli funkci zpětného volání)
		 */
		button.clicked.connect (this.on_button_click);

		/* Přidá tlačítko do tohoto okna a zobrazí jej */
		this.add (button);
		button.show ();
	}

	/* Zpracování signálu pro signál "clicked" tlačítek */
	void on_button_click (Gtk.Button button) {
		var dialog = new Gtk.Dialog.with_buttons ("A Gtk+ Dialog", this,
                                                          Gtk.DialogFlags.MODAL,
                                                          Gtk.Stock.OK,
                                                          Gtk.ResponseType.OK, null);

		var content_area = dialog.get_content_area ();
		var label = new Gtk.Label ("This demonstrates a dialog with a label");

		content_area.add (label);

		/* Napojí signál "response" dialogového okna
		 * na zpracování signálu. Signál je vyslán, kdy je kliknuto
		 * na tlačítko OK v dialogovém okně
		 */
		dialog.response.connect (on_response);

		/* Zobrazí dialogové okno a všechny jeho widgety */
		dialog.show_all ();
	}

	/* Zpracování signálu pro signál "response" dialogového okna */
        void on_response (Gtk.Dialog dialog, int response_id) {

                /* Zobrazí celočíselnou hodnotu ResponseType. Slouží jen
		 * pro ukázkové účely */
                print ("response is %d\n", response_id);

		/* Způsobí likvidaci dialogového okna */
                dialog.destroy ();
        }

}

/* Toto je aplikace */
public class MyApplication : Gtk.Application {

	/* Konstruktor aplikace */
	internal MyApplication () {
		Object (application_id: "org.example.MyApplication");
	}

	/* Přepíše signál "activate" z GLib.Application */
	protected override void activate () {

		/* Vytvoří okno pro tuto aplikaci a zobrazí jej */
		new MyWindow (this).show ();
	}
}

/* Funkce main vytvoří a spustí aplikaci */
public int main (string[] args) {
	return new MyApplication ().run (args);
}
</code>

<p>V této ukázce se používá následující:</p>
<list>
  <item><p><link href="http://www.valadoc.org/gtk+-3.0/Gtk.Dialog.html">Gtk.Dialog</link></p></item>
  <item><p><link href="http://www.valadoc.org/gtk+-3.0/Gtk.Dialog.Dialog.with_buttons.html">Gtk.Dialog.with_buttons</link></p></item>
  <item><p><link href="http://www.valadoc.org/gtk+-3.0/Gtk.Label.html">Gtk.Label</link></p></item>
  <item><p><link href="http://www.valadoc.org/gtk+-3.0/Gtk.Dialog.get_content_area.html">get_content_area</link></p></item>
  <item><p><link xref="response-type"/></p></item>
</list>
</page>
